	.FILE	'NARCPORS.ASM'
	.TITLE	" <<< N  A  R  C  -- THE PORSCHE >>>"

**************************************************************************
*                                                                        *
* 	COPYRIGHT (C) 1988 WILLIAMS ELECTRONICS GAMES, INC. 		 *
* 	ALL RIGHTS RESERVED.						 *
*                                                                        *
**************************************************************************

	.WIDTH	132
	.OPTION	B,D,L
	.MNOLIST

*	GET THE SYSTEM STUFF
	.INCLUDE	"\video\MPROCEQU.ASM"	;MPROC equates
	.INCLUDE	"\video\DISPEQU.ASM"	;Display processor equates
	.INCLUDE	"\video\GSP.INC"	;GSP assembler equates
	.INCLUDE	"\video\SYS.INC"	;Zunit system equates
	.INCLUDE	"\video\MACROS.HDR"	;Macros, Yeah!
*	LOCAL STUFF
	.INCLUDE	"NARCEQU.ASM"		;NARC Equates
	.INCLUDE	"IMGTBL.GLO"		;Image Label Equates
    	.INCLUDE	"NARCFRAG.GLO"
	.INCLUDE	"NARCDRIV.GLO"

	.TEXT
	.INCLUDE	"NARCPORS.TBL"		;Image Label Equates

*
*GLOBALS IN THIS FILE
	.GLOBAL		PCDRIVRC,PORSCHE1,P1IJDECL,P2IJDECL,PDRIVXWW
*
*EXTERNALS
	.GLOBAL		STARTINVL,CFSHRAPB,CFSHRAPA

PORDRVXY EQU	[-10,96]	;PORSCHE PLAYER DRIVER FIRING OFFSET
PORPASXY EQU	[2,81]		;PORSCHE PLAYER PASSENGER FIRING OFFSET
PORDRVRB EQU	[-2,104]	;PORSCHE PLAYER PASSENGER ROCKET BOMB OFFSET
PORPASRB EQU	[9,99]		;PORSCHE PLAYER DRIVER ROCKET BOMB OFFSET
PORFULL	EQU	[17,0]		;PORSCHE FULL SPEED
PORACCEL EQU	[0,4000H]	;PORSCHE ACCELERATION
PORBRAKE EQU	[0,8000H]	;PORSCHE BRAKING DECEL FACTOR
PORZERO	EQU	6		;# OF TICKS TO WAIT AT ZERO VELOCITY
PORSPIN EQU	16		;# OF "INCREMENTS" FOR A SPINOUT

**************************************************************************
*                                                                        *
* PORSCHE - THE DRIVEABLE PORSCHE PROCESS!!!				 *
*                                                                        *
**************************************************************************
PORSCHE
	MOVI	PORDINIT,A14
	MOVI	PORP1P2,A6						 
	CALLA	CARINIT			;INITIALIZE THE CAR
	JAZ	DUMDIE			;BR = INITIALIZATION FAILURE
	CALLA	CARSETZ
	CALLA	CSHADST
	CALLA	CRESET			;RESET THE PARTS AGAIN FOR GOOD MEASURE

	MOVE	@PORCNT,A6,W
	INC	A6			;INITIALIZATION COMPLETE, KNOCK IT UP
	MOVE	A6,@PORCNT,W

	MOVI	PORDRVXY,A6
	MOVE	A6,*A13(EDRIVXY),L
	MOVI	PORPASXY,A6
	MOVE	A6,*A13(EPASSXY),L
	MOVI	PORDRVRB,A6
	MOVE	A6,*A13(EDRIVRB),L
	MOVI	PORPASRB,A6
	MOVE	A6,*A13(EPASSRB),L
	CLR	A6
	MOVE	A6,@PCBSNDF,W
	MOVE	A6,@PCSPINH,W
	MOVK	PORZERO,A6
	MOVE	A6,@PCZEROCT,W
PORSCHEL
	MOVE	A8,A8
	JRZ	PORSCHED			;BR = NO DRIVER, STOP US NOW!
	MOVE	*A8(OID),A0,W
	CMPI	P1OID,A0
	JRNE	PORSCHN1
	MOVE	@P1DATA+PFLAGS,A2,L
	JRUC	PORSCHCS
PORSCHN1
	CMPI	P2OID,A0
	JRNE	PORSCHED			;BR = WHO IS THIS GUY?
	MOVE	@P2DATA+PFLAGS,A2,L
PORSCHCS
	BTST	BSITIN,A2
	JRNE	PORSCHED			;BR = DRIVER NOT SEATED YET
	BTST	BDHOLD,A2
	JRNE	PORSCHED			;BR = DRIVER IS HIT
	BTST	BDEAD,A2
	JRNE	PORSCHED			;BR = DRIVER IS DEAD
	CMPI	P1OID,A0
	JRNE	PORDP2
	MOVI	PORP1P2,A6
	JRUC	PORDTAB
PORDP2
	MOVI	PORP2P1,A6
PORDTAB
	MOVE	A6,*A13(EMAINT),L	;STUFF THE CORRECT ANIMATION TABLE
	MOVE	*A13(ESWTCH),A7,L	;GET THE SWITCH BITS
	MOVE	A7,A0
	SLL	28,A0
	JRC	PSPINOUT
	CLR	A1
	MOVE	A1,@PCSPINH,W		;CLEAR SPIN ONE SHOT FLAG
PORSCHEC
	SRL	23,A0
	MOVI	STEERTAB,A1
	ADD	A0,A1
	MOVE	*A1,A1,L
	JSRPR	A1
PORSCHEJ
	CALLA	CKCRESET		;CHECK IF IT'S TIME TO DO A RESET
	SLOOP	2,PORSCHEL		;HANG OUT DUDE
PORSCHED
	MOVI	0,A11
	MOVE	*A10(OXVEL),A0,L
	JREQ	PORSCHES
	JRGT	PORSCHD1
	MOVI	4300H,A9
	JRUC	PORSCHD2
PORSCHD1
	MOVI	-4300H,A9
PORSCHD2
	CALLA	CXACCELA
PORSCHES
	MOVE	*A10(OYVEL),A0,L
	JREQ	PORSCH0Y
	CLR	A1
	CALLA	CARYZVEL						 
PORSCH0Y
	MOVE	*A10(OXPOS),A1,W
	MOVE	@WORLDTLX+16,A2,W
	SUB	A1,A2
	JRN	PORSCHIF		;BR = PORSCHE IS IN FRONT OF PLAYERS
	CMPI	3*512,A2
	JRHS	PORSCHEX		;BR = PORSCHE TOO FAR BACK, DELETE.
PORSCHIF
	CALLA	CKCRESET		;CHECK IF IT'S TIME TO DO A RESET
	SLOOP	5,PORSCHEL

PORSCHEX
	CALLA	KILLCARP
	CALLA	DELCBODY
	CALLA	CDELALL			;DELETE THE COMPANY
	MOVE	@PORCNT,A6,W
	DEC	A6
	MOVE	A6,@PORCNT,W
	DIE

*PORSCHE SPINOUT, A0 = JOYSTICK BITS SLL'D 28
PSPINOUT
	MOVE	@PCSPINH,A1,W
	JRNZ	PORSCHEC		;RETURN, ONE SHOT IS ON
	MOVK	1,A1
	MOVE	A1,@PCSPINH,W		;SET THE SPIN HOLD
	MOVE	A0,A1
	CLR	A11
	MOVI	FRICTION*2,A9
	MOVE	*A10(OXVEL),A0,L
	JRZ	PSPINJ
      	JRN	PSPIND
	NEG	A9
PSPIND
	CALLA	CXACCELA
PSPINJ
	SOUND1	PCSPINSD
	MOVE	A0,@PCBSNDF,W		;SET SOUND FLAG
	SLL	3,A1			;CHECK DOWN STICK
	JRC	PSPINR	
      	SLL	1,A1			;STICK UP CHECK
	JRC	PSPINL
	MOVE	*A10(OFLAGS),A1,W
	BTST	B_FLIPH,A1	
	JRNE	PSPINR
PSPINL
	CALLA	ISREVERS
	JRC	PSPINR1			;BR = REVERSED, DO THE OPPOSITE
PSPINL1
	MOVE	*A10(OYVEL),A1,L
	SUBI	4000H,A1
	CALLA	CARYZVEL
	JSRP	CLTURN
	JSRP	CLEFT180
	JRUC	PORSCHEJ
PSPINR
	CALLA	ISREVERS
	JRC	PSPINL1			
PSPINR1
	MOVE	*A10(OYVEL),A1,L
	ADDI	4000H,A1
	CALLA	CARYZVEL
	JSRP	CRTURN
	JSRP	CRYTE180
	JRUC	PORSCHEJ

STEERTAB
	.LONG	STRAIGHT,TURNLEFT,TURNRYTE,DUMRETP,REVERSE,REVLEFT
	.LONG	REVRYTE,DUMRETP,FORWARD,FORLEFT,FORRYTE,DUMRETP
	.LONG	DUMRETP,DUMRETP,DUMRETP,DUMRETP

*NOTE: ALL OF THESE DIRECTIONAL ROUTINES MUST BE CALLED WITH JSRP
*MAKE THE CAR GO STRAIGHT AHEAD
STRAIGHT
	CLR	A1
	CALLA	CARYZVEL
	MOVK	PORZERO,A9
	MOVE	A9,@PCZEROCT,W		;RE-INITIALIZE THE COUNT
	MOVI	FRICTION,A9
	CLR	A11
	MOVE	*A10(OXVEL),A0,L
	JRN	STRAIG3
	NEG	A9
STRAIG3
	CALLA	CXACCELA		;DECELERATE DUE TO FRICTION
STRAYTEX
	CLR	A9
	MOVE	@PCBSNDF,A1,W
	JAZ	CSTRAYTE		;NO BRAKING SOUND GOING DOWN
	SOUND1	PCALLOFF		;SHUT DOWN BRAKE SOUND
	MOVE	A9,@PCBSNDF,W		;DOWN THE FLAG
	JAUC	CSTRAYTE

*REVERSE VELOCITY	
REVERSE
	CLR	A1
	CALLA	CARYZVEL						 
	MOVI	-PORACCEL,A1
	MOVI	-PORBRAKE,A2
	MOVI	-PORFULL,A3
	CALLR	SETRVEL
	JRNC	STRAYTEX
	CLR	A9
	JAUC	CSTRAYTE
*FORWARD VELOCITY	
FORWARD
	CLR	A1
	CALLA	CARYZVEL						 
	MOVI	PORACCEL,A1
	MOVI	PORBRAKE,A2
	MOVI	PORFULL,A3
	CALLR	SETFVEL
	JRNC	STRAYTEX
	CLR	A9
	JAUC	CSTRAYTE
*FORWARD VELOCITY,LEFT TURN	
FORLEFT
	MOVE	*A10(OXVEL),A1,L
	SRA	3,A1
	JRN	FORL1
	NEG	A1
FORL1
	CALLA	CARYZVEL						 
	MOVI	PORACCEL/2,A1
	MOVI	PORBRAKE,A2
	MOVI	PORFULL,A3
	CALLR	SETFVEL
	JRC	CLEFTBA9		;BRAKING, TRY TO SPIN
	JRUC	CLEFTA9
*FORWARD VELOCITY,RIGHT TURN	
FORRYTE
	MOVE	*A10(OXVEL),A1,L
	SRA	3,A1
	JRNN	FORR1
	NEG	A1
FORR1
	CALLA	CARYZVEL						 
	MOVI	PORACCEL/2,A1
	MOVI	PORBRAKE,A2
	MOVI	PORFULL,A3
	CALLR	SETFVEL
	JRC	CRYTEBA9		;BRAKING, TRY TO SPIN
	JRUC	CRYTEA9
*REVERSE VELOCITY,LEFT TURN	
REVLEFT
	MOVE	*A10(OXVEL),A1,L
	SRA	3,A1
	JRN	REVL1
	NEG	A1
REVL1
	CALLA	CARYZVEL						 
	MOVI	-(PORACCEL/2),A1
	MOVI	-PORBRAKE,A2
	MOVI	-PORFULL,A3
	CALLR	SETRVEL
	JRC	CLEFTBA9		;BRAKING, MUST TO MAKE NOISES
	JRUC	CLEFTA9
*REVERSE VELOCITY,RIGHT TURN	
REVRYTE
	MOVE	*A10(OXVEL),A1,L
	SRA	3,A1
	JRNN	REVR1
	NEG	A1
REVR1
	CALLA	CARYZVEL						 
	MOVI	-(PORACCEL/2),A1
	MOVI	-PORBRAKE,A2
	MOVI	-PORFULL,A3
	CALLR	SETRVEL
	JRC	CRYTEBA9		;BRAKING, SOUND OFF
	JRUC	CRYTEA9

*TURN THE CAR LEFT
TURNLEFT
	MOVE	*A10(OXVEL),A1,L
	MOVE	A1,A9
	SRA	3,A1
	JRN	TURNL3
	NEG	A1
TURNL3
	CALLA	CARYZVEL						 
	JRUC	CLEFTA9							 

*TURN THE CAR RIGHT
TURNRYTE
	MOVE	*A10(OXVEL),A1,L
	MOVE	A1,A9
	SRA	3,A1
	JRNN	TURNR3
	NEG	A1
TURNR3
	CALLA	CARYZVEL						 
	JRUC	CRYTEA9							 
*
*EXIT FOR LEFT TURN, NO BRAKING
CLEFTA9
	MOVE	@PCBSNDF,A1,W
	JAZ	CLTURN		;NO BRAKING SOUND GOING DOWN
	SOUND1	PCALLOFF
*EXIT POINT FOR ANY LEFT TURN
CLEFTBA9
	JAUC	CLTURN
*
*EXIT FOR RIGHT TURN, NO BRAKING
CRYTEA9
	MOVE	@PCBSNDF,A1,W
	JAZ	CRTURN		;NO BRAKING SOUND GOING DOWN
	SOUND1	PCALLOFF
*EXIT POINT FOR ANY RIGHT TURN
CRYTEBA9
	JAUC	CRTURN

**************************************************************************
*                                                                        *
* SETFVEL - SET PLAYER CAR FORWARD VELOCITY				 *
* A1 = ACCELERATION FACTOR						 *
* A2 = BRAKING FACTOR							 *
* A3 = TOP SPEED							 *
* A8 = PTR TO DRIVER							 *
* A10 = PTR TO CAR							 *
* A13 = PTR TO CAR PROCESS						 *
* RETURNS:								 *
* Z BIT SET = STOPPED							 *
* Z BIT CLR = IGNORE							 *
* C BIT SET = BRAKING							 *
* C BIT CLR = ACCELERATING						 *
*                                                                        *
**************************************************************************
SETFVEL
	MMTM	SP,A0,A9,A11
	MOVE	*A10(OXVEL),A11,L
	JRGT	SETFVEL1
	JRNZ	SETFVEL0
	MOVE	@PCZEROCT,A9,W
	CMPI	PORZERO,A9
	JRGE	SETFVEL1
	INC	A9
	MOVE	A9,@PCZEROCT,W
	JRUC	SETFVELX	
SETFVEL0
	CLR	A11
	MOVE	A2,A9			;SKID BRAKE
	CALLA	CXACCELB
	CALLA	CWHLSTOP
	MOVE	@PCBSNDF,A0,W
	JRNZ	SETFVELN
	SOUND1	PCBRAKSD
	MOVE	A0,@PCBSNDF,W		;FLAG BRAKE SOUND
SETFVELN
	SETC				;FLAG BRAKE
	JRUC	SETFVEL2
SETFVEL1
	MOVE	@UNIVENDX,A11,W
	SUBI	512,A11
	MOVE	*A10(OXPOS),A9,W
	CMP	A11,A9
	JRLT	SETFVELOK
	MOVI	[5,0],A11
	CALLA	XVEL2XA
	CLRC
	JRUC	SETFVEL2
SETFVELOK
	MOVE	A3,A11
	MOVE	A1,A9			;ACCELERATE
	CALLA	CXACCELA
	CLRC				;FLAG ACCELERATE
SETFVEL2
	CLR	A9
	MOVE	A9,@PCZEROCT,W
	MOVK	1,A9
	MOVE	A9,A9			;CLR Z BIT WITHOUT CLRING C
SETFVELX
	MMFM	SP,A0,A9,A11
	RETS
	
**************************************************************************
*                                                                        *
* SETRVEL - SET PLAYER CAR FORWARD VELOCITY				 *
* A1 = ACCELERATION FACTOR						 *
* A2 = BRAKING FACTOR							 *
* A3 = TOP SPEED							 *
* A8 = PTR TO DRIVER							 *
* A10 = PTR TO CAR							 *
* A13 = PTR TO CAR PROCESS						 *
* RETURNS:								 *
* Z BIT SET = STOPPED							 *
* Z BIT CLR = IGNORE							 *
* C BIT SET = BRAKING							 *
* C BIT CLR = ACCELERATING						 *
*                                                                        *
**************************************************************************
SETRVEL
	MMTM	SP,A0,A9,A11
	MOVE	*A10(OXVEL),A11,L
	JRGT	SETRVEL1
	JRNZ	SETRVEL0
	MOVE	@PCZEROCT,A9,W
	CMPI	PORZERO,A9
	JRGE	SETRVEL0
	INC	A9
	MOVE	A9,@PCZEROCT,W
	JRUC	SETRVELX	
SETRVEL0
	MOVE	@UNIVSTX,A11,W
	ADDI	512,A11
	MOVE	*A10(OXPOS),A9,W
	CMP	A11,A9
	JRGT	SETRVELOK
	MOVI	[-5,0],A11
	CALLA	XVEL2XA
	CLRC
	JRUC	SETRVEL2
SETRVELOK
	MOVE	A3,A11
	MOVE	A1,A9			;ACCELERATE
	CALLA	CXACCELA
	CLRC				;FLAG ACCELERATE
	JRUC	SETRVEL2
SETRVEL1
	CLR	A11
	MOVE	A2,A9			;BRAKE
	CALLA	CXACCELB
	CALLA	CWHLSTOP
	MOVE	@PCBSNDF,A0,W
	JRNZ	SETRVELN
	SOUND1	PCBRAKSD
	MOVE	A0,@PCBSNDF,W		;FLAG BRAKE SOUND
SETRVELN
	SETC				;FLAG BRAKE
SETRVEL2
	CLR	A9
	MOVE	A9,@PCZEROCT,W
	MOVK	1,A9
	MOVE	A9,A9			;CLR Z BIT WITHOUT CLRING C
SETRVELX
	MMFM	SP,A0,A9,A11
	RETS

REVREV
	.WORD	>F015,>15,>800E,>800E,0	;REV, REV!

**************************************************************************
*                                                                        *
* XVEL2XA - DECELERATE THE CURRENT CAR X VELOCITY TO GIVEN		 *
*		ADJUSTS WHEEL SPEED					 *
* A8 = PTR TO DRIVER							 *
* A10 = PTR TO CAR							 *
* A11 = VELOCITY DESIRED						 *
* A13 = PTR TO CAR PROCESS						 *
*                                                                        *
**************************************************************************
XVEL2XA
	MMTM	SP,A1,A9
	MOVE	*A10(OXVEL),A1,L
	CMP	A11,A1
	JRZ	XVELXISZA
	JRN	XVELXF2SA
	MOVI	-FRICTION,A9
	CALLA	CXACCELA
	JRUC	XVELXISZA
XVELXF2SA
	MOVI	FRICTION,A9
	CALLA	CXACCELA
XVELXISZA
	MMFM	SP,A1,A9
	RETS

**************************************************************************
*                                                                        *
* PORINTRO - THE PORSCHE INTRO PROCESS, FOR WAVE START			 *
* ETEMP1 = PTR TO WINDSHIELD OBJECT					 *
* ETEMP3 = PTR TO FRONT WHEEL OBJECT					 *
* ETEMP4 = PTR TO REAR WHEEL OBJECT					 *
*                                                                        *
**************************************************************************
PORINTRO
	MOVI	PORINITI,A14
	MOVI	PORP2P1,A6						 
	CALLA	CARINIT			;INITIALIZE THE CAR
	JAZ	DUMDIE			;BR = INITIALIZATION FAILURE
	CALLA	CARSETZ
***	CALLA	CSHADST
	MOVE	@PORCNT,A0,W
	INC	A0
	MOVE	A0,@PORCNT,W
	SOUND1	REVREV
PORISTLP
	MOVE	@SCROLLX,A1,L
	JRNZ	PORDODO	
	SLOOP	1,PORISTLP
PORDODO
	SOUND1	SQUEELSR
PORLOOP
	MOVE	@SCROLLX,A1,L		;GET THE CURRENT SCROLL VALUE
	CALLA	CARXVEL			;STUFF MY OWN FOR THE INTRO
	CALLA	CWHLSPD			;AND SET THE SPEED
	MOVE	@WORLDTLX+16,A0,W	;GET THE CURRENT WORLD COORD.
	CMPI	-1024,A0
	JRGT	PORIEND
	CALLA	CKCRESET		;CHECK RESET TIME
	SLOOP	1,PORLOOP

PORIEND
	SLEEP	2

	MOVI	0,A11
	MOVI	-4300H,A9
	CALLA	CXACCEL

	MOVI	SPINSNDR,A0
	CALLA	ONESND

	JSRP	CLTURN							 
 	MOVI	-5000H,A1						 
	CALLA	CARYZVEL						 
	SLEEP	0EH							 
*
*MAKE THE DUDES FLY FROM THE CAR
	MOVI	INPLAY,A0
	MOVE	A0,@GAMSTATE,W		;MARK INTRO AS FINISHED

	SWAP	A10,A8
	CALLA	GETANIXY
	SWAP	A10,A8
	ADDI	[30,0],A3
	MOVE	A2,A9
	MOVE	A3,A11

	MOVI	PJUMP|PRYTE,A7
	MOVE	A7,@P1CTRL,W
	SLEEP	2
	MOVI	P1DATA,A5
	MOVE	*A5(POBJ),A0,L
	JRZ	PORIP2
	MOVE	A0,A6
	MOVE	A9,A2
	MOVE	A11,A3
	MOVI	DMAWNZ,A4
	CALLA	GANISAG			;ADJUST THE TORSO PORTION OF THE BOD
	MOVE	A9,A2
	MOVE	A11,A3
	MOVE	*A5(PLOBJ),A0,L
	CALLA	GANISAG			;NOW THE LEGS
	MOVE	*A10(OXVEL),*A6(OXVEL),L
	MOVE	*A10(OXVEL),*A0(OXVEL),L
	CREATE	INDPID,P1IJDECL

	MOVI	CARLIST,A1
	MOVE	*A13(PASSPTR),A0,L
	JRZ	PORIP2
	CALLA	DELSUPP
	CALLA	DELSOBJ
	CLR	A0
	MOVE	A0,*A13(PASSPTR),L
PORIP2
	MOVI	PJUMP|PRYTE,A7
	MOVE	A7,@P2CTRL,W
	SLEEP	2
	MOVI	P2DATA,A5
	MOVE	*A5(POBJ),A0,L
	JRZ	PORGHOST
	MOVE	A0,A6
	MOVE	A9,A2
	MOVE	A11,A3
	MOVI	DMAWNZ,A4
	CALLA	GANISAG			;ADJUST THE TORSO PORTION OF THE BOD
	MOVE	A9,A2
	MOVE	A11,A3
	MOVE	*A5(PLOBJ),A0,L
	CALLA	GANISAG			;NOW THE LEGS
	MOVE	*A10(OXVEL),*A6(OXVEL),L
	MOVE	*A10(OXVEL),*A0(OXVEL),L
	CREATE	INDPID,P2IJDECL

	MOVI	ENEMIES,A1
	MOVE	A8,A0
	JRZ	PORGHOST
	CALLA	DELSUPP
	CALLA	DELSOBJ
	CLR	A8
PORGHOST
	JSRP	CLEFT360						 
  	CLR	A1
 	CALLA	CARYZVEL
	CALLA	WHLSTR
	SLEEP	60H		;SLEEP AT LEAST THIS LONG
PORIEND1
	SLEEP	3
	MOVE	@GAMSTATE,A0,W
	CMPI	INPLAY,A0
	JRNE	PORIEND1
	MOVI	[-2,0],A11
	MOVI	-1000H,A9
	CALLA	CXACCEL		;BACK THE DUDE OFF SCREEN
	
PORIENDX
	SLEEP	6
	PUSH	A8
	MOVE	A10,A8
	CALLA	SCRTST
	MMFM	SP,A8
	JREQ	PORIENDX   	;BR = STILL ON SCREEN	 
	CALLA	KILLCARP
PORINTX
	CALLA	DELCBODY
	CALLA	CDELALL			;DELETE THE COMPANY
	MOVE	@PORCNT,A0,W
	DEC	A0
	MOVE	A0,@PORCNT,W
	DIE

*
*MAKE PLAYERS THAT JUMP FROM A CAR DECEL CORRECTLY
*PLAYER 1
P1IJDECL
	MOVI	P1DATA,A2
	MOVI	P1CTRL,A11
	JRUC	PLJDECEL
*PLAYER 2
P2IJDECL
	MOVI	P2DATA,A2
	MOVI	P2CTRL,A11
PLJDECEL
	MOVE	*A2(POBJ),A8,L
	MOVE	*A2(PLOBJ),A9,L
	MOVE	*A10(OXVEL),A10,L
	MOVI	01000H,B0
	MOVI	08000H,B1
	CALLA	RANGRAND
	ADD	A0,A10				;DIFFERENCE IN SPEED
	SLEEP	6
	MOVE	A10,*A8(OXVEL),L
	MOVE	A10,*A9(OXVEL),L
	MOVE	*A8(OYVEL),A3,L
	ADDI	[-2,0],A3
	MOVE	A3,*A8(OYVEL),L
	MOVE	A3,*A9(OYVEL),L
	SLEEP	4
PLJSLP
	SUBI	3000H,A10
	MOVE	*A8(OXVEL),A1,L
	ABS	A1
	CMPI	[2,1000H],A1
	JRLE	PLJDECX
	MOVE	A10,*A8(OXVEL),L
	MOVE	A10,*A9(OXVEL),L
	SLOOP	1,PLJSLP
PLJDECX
	MOVI	PRYTE,A0
	MOVE	A0,*A11,W
	MOVK	16,A3
	CMPI	P1CTRL,A11
	JREQ	PLJCON
	MOVI	P1OID,A0
	MOVI	PLAYERS,A1
	CALLA	ISSUPPID
	JRZ	PLJCON			;BR = NO PLAYER 1, JUST 16 TICKS	
	ADDK	16,A3			;MORE STEPS FOR PLAYER 2
PLJCON
	SLEEPR	A3
	CLR	A0
	MOVE	A0,*A11,W
	DIE
	
	
**************************************************************************
*                                                                        *
* PORTRANS - THE PORSCHE TRANSPORT PROCESS FOR PLAYER BUY-IN.		 *
* A8 = PTR TO PLAYER TORSO OBJECT (UNINSERTED).				 *
* A9 = PTR TO PLAYER LEG OBJECT (UNINSERTED).				 *
* A11 = PTR TO PLAYER DATA STRUCTURE.					 *
*                                                                        *
**************************************************************************
PORTRANS
	CMPI	P1DATA,A11
	JRNE	PTRANSP2
	MOVI	PORTRAN1,A14
	JRUC	PTRANSPG
PTRANSP2
	MOVI	PORTRAN2,A14
PTRANSPG
	MMTM	A12,A8,A9		;PUSH PLAYER OBJECT PTRS
	MOVI	PORP2P1,A6						 
	CALLA	CARINIT			;INITIALIZE THE CAR
	JRZ	PTRANERR		;ERROR ON CAR INITIALIZATION
	CALLA	CSHADST
	CALLA	CARSETZ
	MOVE	@PORCNT,A0,W
	INC	A0
	MOVE	A0,@PORCNT,W

	MOVI	8000H,A9
PTRANS1
	MMTM	SP,A11
	MOVI	[6,0],A11
	CALLA	CXACCEL
	MMFM	SP,A11
	JRNZ	PTRANS2
	SLOOP	1,PTRANS1
PTRANS2
****	MOVE	A8,A0
****	CALLA	GSCRNREL		;BASHES A1 & A2
****	SRA	16,A1
****	CMPI	150,A1
****	JRGT	PTRANS4
****	CALLA	CKCRESET		;CHECK FOR CAR RESET TIME
****	SLOOP	6,PTRANS2
	SWAP	A10,A8			;WATCH FOR PORSCHE TO GET TO LAUNCH SPOT
	CALLA	GETANIX
	SWAP	A10,A8
	MOVE	@WORLDTLX+16,A1,W
	SUB	A1,A0			;MAKE THE X COORDINATE SCREEN RELATIVE
	CMPI	150,A0
	JRGT	PTRANS4
	CALLA	CKCRESET
	MOVE	A8,A8
	JRNZ	PTRANS3			;THE DRIVER IS GONE FOR SOME REASON
	MOVI	DUMCOLL,A0
	MOVE	A0,*A10(OCVECT),L	;NULL THE COLLISION VECTOR JUST IN CASE
PTRANS3
	SLOOP	6,PTRANS2

PTRANS4
	MOVI	ENEMIES,A1
	MOVE	A8,A0
	JRZ	PTRANS40
	CALLA	DELSUPP
	CALLA	DELSOBJ			;DELETE PLACEHOLDER
PTRANS40
	MOVE	A10,A8
	CALLA	GETANIX

	CMPI	P1DATA,A11
	JRNE	PTRANS5
	MOVI	P1CTRL,A9
	JRUC	PTRANS6
PTRANS5
	MOVI	P2CTRL,A9	
PTRANS6
	MOVI	PJUMP|PRYTE,A7
	MOVE	A7,*A9,W
	MMFM	A12,A3,A4
	MOVE	A3,A8				;GOT THE OBJECT PTRS BACK
	MOVE	*A8(OXPOS),A5,W			;WARP PLAYER TO DRIVER SPOT	
	MOVE	A0,*A8(OXPOS),W
	SUB	A5,A0
	MOVE	*A4(OXPOS),A5,W
	ADD	A0,A5
	MOVE	A5,*A4(OXPOS),W

	MOVE	A8,A0
	MOVI	PLAYERS,A1						 
	CALLA	INSOBJ			;INSERT TORSO			 
	CALLA	ADDSUPP			;ADD TO SUPPLEMENTAL LIST	 
	MOVE	A4,A0							 
	CALLA	INSOBJ			;INSERT LEGS 			 
	CALLA	ADDSUPP			;ADD TO SUPPLEMENTAL LIST	 
	CALLA	SHADST2			;START SHADOW FOR PLAYER TORSO	 
	MOVE	A4,A8							 
	CALLA	SHADST			;START SHADOW FOR PLAYER LEGS	 
	MOVE	*A11(PEXEFLAG),A0,W
	ORI	MINVINC,A0		;MAKE HIM INVINCIBLE
	MOVE	A0,*A11(PEXEFLAG),W
PWWL
	CLR	A8			;INDICATE NO DRIVER PRESENT
	CREATE	INDPID,PTRANWW
    	JRNZ	PWWGO
	SLOOP	1,PWWL
PWWGO
	SLEEP	6
	PUSH	A8
	MOVE	A10,A8
	CALLA	SCRTST
	MMFM	SP,A8
	JREQ	PWWGO		   	;BR = STILL ON SCREEN	 
	CALLA	KILLCARP
	CALLA	DELCBODY
	CALLA	CDELALL			;DELETE THE COMPANY
	MOVE	@PORCNT,A0,W
	DEC	A0
	MOVE	A0,@PORCNT,W
	DIE
*
*ERROR ENTRY FOR PLAYER TRANSPORT PORSCHE
*LEG PTR, AND TORSO PTR ARE ON PROC STACK RESPECTIVELY
*A11 = PLINDX
PTRANERR
	MMFM	A12,A8,A9
	MOVE	A11,A2
	MOVE	A9,A4			;FOR CONVIENCE
	CALLA	STARTINVL
	MOVI	PLAYERS,A1						 
	MOVE	A8,A0			;MAKE SURE TORSO POINTER IS CORRECT
	CALLA	ADJSTWTL
	CALLA	INSOBJ			;INSERT TORSO			 
	CALLA	ADDSUPP			;ADD TO SUPPLEMENTAL LIST	 
	MOVE	A4,A0							 
	CALLA	ADJSTWTL
	CALLA	INSOBJ			;INSERT LEGS 			 
	CALLA	ADDSUPP			;ADD TO SUPPLEMENTAL LIST	 
	CALLA	SHADST2			;START SHADOW FOR PLAYER TORSO	 
	MOVE	A4,A8							 
	CALLA	SHADST			;START SHADOW FOR PLAYER LEGS	 
	DIE
*
*PTRANWW - PLAYER TRANSPORT WALK WATCHER PROCESS
*A9 = PTR TO PxCTRL
*A11 = PTR TO PxDATA
PTRANWW
	SLEEP	66
	CLR	A0
	MOVE	A0,*A9,W
	MOVI	PINVTIM,A10		;THIS IS THE FLASH COUNT
	MOVE	A11,A9
	MOVE	*A11(PPID),A1,W
	ADDI	PIPID,A1
	MOVE	*A11(POBJ),A8,L
	MOVE	A11,A14
	MOVE	*A11(PLOBJ),A11,L
	MOVI	INVINCO,A7
	CALLA	GETPRC			;DO THE INVICIBLE THING
	JRNZ	PTRANWWX		;BR = INVINCO PROCESS CREATED O.K.	
	MOVE	*A14(PEXEFLAG),A0,W
	ANDNI	MINVINC,A0		;CLEAR FLAG IF PROCESS WASN'T CREATED
	MOVE	A0,*A14(PEXEFLAG),W
PTRANWWX
	DIE


**************************************************************************
*                                                                        *
* PORSCOLD - CREATE A DRIVEABLE PORSCHE.				 *
*                                                                        *
**************************************************************************
PORSCOLD
	MOVE	@PORCNT,A0,W
	JRNE	PORSCX			;ONLY ONE AT A TIME DUDELY
	CREATE	PORSDPID,PORSCHE
PORSCX
	RETS

*INIT TAB FOR PINK CAR PIECES
REDINIT
	.LONG	0,0,0,0
	.WORD	0,0
	.LONG	CFRAG1,0,DUMCOLL
	.WORD	DMAWNZ,0
	.LONG	0

**************************************************************************
*                                                                        *
* PORSCHE EXPLOSION PROCESS, THIS IS TIMED TO SYNC UP WITH THE CEXPLODE *
*  WHICH RUNS JUST THE FIRE PART OF THE EXPLOSION. THIS PROCESS IS RESPO *
*  FOR DESTROYING THE CAR AND SENDING LITTLE TINY PIECES OF SHRAPNEL ALL *
*  OVER THE PLACE.							 *
* A8 = POINTER TO THE PORSCHE OBJECT					 *
* A9 = OID OF ROCKET							 *
* I.D. = $ED                                                             *
**************************************************************************
POREXP
	CALLA	PSTOP
	CALLA	BLIPOFF			;TURN THE SCANNER BLIP OFF
	MOVE	A8,A0
	CALLA	DELSHAD
	SRL	4,A9
	SLL	4,A9
	CMPI	B1OID,A9
	JRNE	POREXP51
	MOVI	P1DATA,A2
	JRUC	POREXP5K
POREXP51
	CMPI	B2OID,A9
	JRNE	CADENOK
	MOVI	P2DATA,A2
POREXP5K
	MOVI	5000H,A1
	CALLA	SCORE			;5K FOR CADILLAC EXPLOSION
CADENOK
	ADDK	EXPLODID,A9		;MAKE THIS SOME SORT OF EXPLOSION
	MOVE	A9,*A8(OID),W		;MAKE THE CAR AN EXPLOSION.

***	CALLA	GETOBJ			;GET A BLOCK FOR THE RIGHT SHELL
***	JRNE	CADECON			;BR = CREATE O.K., CONTINUE
***	MOVE	A8,A0
***	CALLA	DELPAL
***	CALLA	DELSOBJ			;FAILURE, DELETE AND DIE
***	DIE

CADECON
***	CALLA	COPYOBJ			;TRANSFER DATA TO THE RIGHT SHELL BLOCK
***	MOVE	*A8(OXPOS),A11,W
***	MOVE	*A8(OYPOS),A14,W
***	SLL	16,A14
***	MOVY	A14,A11			;A11 = YPOS:XPOS

***	MOVI	CSHELLR,A1
***	MOVE	*A0(OFLAGS),A4,W
***	MOVE	A8,A10
***	MOVE	A0,A8
***	CALLA	ANI			;SET-UP SECOND HALF
*******	CALLA	OBJOFF			;TURN THIS OBJECT OFF
***	CALLA	INSOBJ			;AND INSERT IT
***	MOVE	A10,A8
***	MOVE	A0,A10			;KEEP HERE FOR SLEEPS

	MOVI	POREXPFR,A9		;GET THE EXPLOSION FRAMES
	CLR	A1
	JSRP	FRANIM			;FIRST FRAME

	MOVI	JUNKOID,A0		;GET THE GARBAGE OID
	MOVE	A0,*A8(OID),W
***	MOVE	A0,*A10(OID),W

	MOVE	@OVERLOAD,A0
	CMPI	4,A0
	JREQ	FRAGSKIP		;BR = OVERLOAD, SKIP THE FRAGS

* START ALL THE FRAGMENTS HERE

	MMTM	SP,A9,A10,A11
	CALLA	GETCPNT			;GET ENEMY'S CENTER
	MOVY	A1,A11
	MOVX	A1,A11			;SET UP A STARTING POSITION
	MOVE	A11,A5			;SAVE CENTER POSITION 
	MOVI	TRAJEC,A7		;PROCESS FOR FRAGMENTS AND DOOR
	CLR	A1			;ZERO I.D.
	MOVE	*A8(OZPOS),A14,W
	MOVI	DMAWNZ,A0	
	SLL	16,A0
	MOVX	A14,A0			;A0 = [DMA FLAGS,ZPOS]
	MOVE	A0,B5			;CONSTANT THROUGHOUT FRAGMENT CREATION
	MOVI	DBOUNCE,A14		;SAME HERE		
	CLR	B1			;NO 2nd ANIMATION SCRIPT

*SEND FRAGMENTS TO THE RIGHT OF THE EXPLOSION CENTER
	MOVK	6,A6			;THIS MANY PIECES
FRAGR
	CALLA	GETFRAG			;GET FRAGMENT AND IT'S Y VELOCITY
FRAGR3
	MOVI	50,A0 			;RANDOMIZE POSITION & X VELOCITY
	CALLA	RANDU
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	12,A0
	JREQ	FRAGR3	  		;NO ZEROZ
	MOVE	A0,B4
	CALLA	MAKTPROC
	MOVE	A5,A11			;RESTORE CENTER
	DSJ	A6,FRAGR
*SEND FRAGMENTS TO THE LEFT OF THE EXPLOSION CENTER
	MOVK	6,A6			;THIS MANY PIECES
FRAGL
	CALLA	GETFRAG
FRAGL3
	MOVI	50,A0 			;RANDOMIZE POSITION & VELOCITY
	CALLA	RANDU
     	NEG	A0			;THESE GUYS GO LEFT
	ZEXT	A0			;BLOW OFF THE SIGN
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SEXT	A0			;RE-SIGN FOR DIVIDE
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	13,A0
	JREQ	FRAGL3	  		;NO ZEROZ
	MOVE	A0,B4
	CALLA	MAKTPROC
	MOVE	A5,A11			;RESTORE CENTER
	DSJ	A6,FRAGL
*SEND THE DOORS FLYING
DOORFRAG
	MOVI	CDOORA,A9		;FRAMES
	MOVI	REDINIT,A10
     	MOVK	2,A6
EDOOR
	CALLA	GETYZVEL		;GET THE Y AND Z VELOCITIES
	MOVK	3,A0
	CALLA	RANDU
	BTST	0,A0
	JREQ	EDOOR1
	NEG	A0
EDOOR1
	SLL	16,A0
	MOVE	A0,B4			;XVEL IS LOADED
	CALLA	MAKTPROC
	DSJ	A6,EDOOR
*SEND THE TIRES
CEXPTIRE
	MOVI	PORWEEL1CC,A9		;FRAMES
	MOVI	DTBOUNCE,A14		;DO A TIRE BOUNCY 'TING		
*TIRES TO THE RIGHT OF ME
CEXPTR1
	CALLA	GETYZVEL		;GET THE Y AND Z VELOCITIES
CEXPTR2
	MOVI	50,A0 			;RANDOMIZE POSITION & X VELOCITY
	CALLA	RANDU
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	12,A0
	JREQ	CEXPTR2	  		;NO ZEROZ
	MOVE	A0,B4			;XVEL IS LOADED
	CALLA	MAKTPROC
	MOVE	A5,A11
*TIRES TO THE LEFT...
CEXPTL3
	CALLA	GETYZVEL		;GET THE Y AND Z VELOCITIES
CEXPTL4
	MOVI	50,A0 			;RANDOMIZE POSITION & VELOCITY
	CALLA	RANDU
     	NEG	A0			;THESE GUYS GO LEFT
	ZEXT	A0			;BLOW OFF THE SIGN
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SEXT	A0			;RE-SIGN FOR DIVIDE
	ADDXY	A0,A11			;ADD FOR NEW POSITION
	SLL	13,A0
	JREQ	CEXPTL4	  		;NO ZEROZ
	MOVE	A0,B4			;XVEL IS LOADED
	CALLA	MAKTPROC
	MOVE	A5,A11
	MMFM	SP,A9,A10,A11
*CONTINUE WITH THE CADILLAC EXPLOSION
FRAGSKIP
	SLEEP	1

***	MOVE	A10,A0
***	CALLA	OBJON

	CLR	A1
	JSRP	FRANIM			;DO SHELL FRAME

	SLEEP	20

***	MOVE	A10,A0
***	CALLA	DELSOBJ			;DELETE SHELL RIGHT HALF

	CLR	A1
	JSRP	FRANIM			;STUFF THE WRECKAGE
	SLEEP	10
	JAUC	SINKCARC

**************************************************************************
*                                                                        *
* BLOWSHLD - BLOW OUT THE WINDSHIELD OF THE PORSCHE PROCESS.		 *
* A8 = PTR TO THE WINDSHIELD OBJECT					 *
* A11 = OID OF KILLER							 *
* NOTE: THE BLOWOUT OBJECT MUST BE CREATED, STUFFED, AND ON THE LIST.	 *
*                                                                        *
**************************************************************************
BLOWSHLD
	SOUND1	CADSHLD		;MAKE A BLOWOUT NOISE
	MOVI	PBLOWWS,A9	;LOAD ANIMATION SCRIPT
	SRL	4,A11
	SLL	4,A11
	CMPI	B1OID,A11
	JRNE	BBLOWS1
	MOVI	P1DATA,A2
	JRUC	BBLOWS2
BBLOWS1
	CMPI	B2OID,A11
	JRNE	BLOWAGN
	MOVI	P2DATA,A2
BBLOWS2
	MOVI	250H,A1
	CALLA	SCORE		;250 FOR HITTING THE WINDSHIELD
BLOWAGN
	MOVK	1,A1
	JSRP	FRANIM		;ANIMATE IT
	CLR	A0
	MOVE	A0,*A8(OPLINK),L
	DIE

**************************************************************************
*                                                                        *
* GETFRAG - GETS A RANDOM CAR FRAGMENT					 *
*	    AND ITS Y & Z VELOCITIES, FOR A BEAUTY TRAJECTORY.		 *
* A8 = PTR TO OBLOCK							 *
* RETURN(S)								 *
* A9 = PTR TO FRAGMENT ANIMATION SCRIPT					 *
* A10 = PTR TO CORRECT INIT TABLE FOR MAKTPROC				 *
* B2 = Z VELOCITY (32 BITS)						 *
* B3 = Y VELOCITY (32 BITS)						 *
*                                                                        *
**************************************************************************
GETFRAG
	PUSH	A0
	CALLA	GETYZVEL		;GET A RANDOM Y & Z VELOCITIES

	BTST	0,B3			  			
	JRNE	GETFRAG2		;ODD FRACTION, GIVE ME NORMAL STUFF
	CLR	A10
	TEST	B2			;PIECES ACCORDING TO Z VELOCITY
	JRN	GETFRAG1		;SHRINK WHILE GOING AWAY

	MOVI	CFSHRAPA,A9
	JRUC	GETFRAG4

GETFRAG1
	MOVI	CFSHRAPB,A9
	JRUC	GETFRAG4

GETFRAG2
	MOVI	REDINIT,A10
	MOVE	B3,A0
	ABS	A0
	SRL	18,A0
	JRZ	GETFRAG3		;LOW VELOCITY PIECE

	MOVI	CSHRAPA,A9	
	JRUC	GETFRAG4

GETFRAG3
	MOVI	CSHRAPB,A9

GETFRAG4
	PULL	A0
	RETS

**************************************************************************
*                                                                        *
* PORCOLR - PORSCHE MAIN, COLLISION WITH ROCKET BOMB			 *
* A0 = PTR TO PORSCHE MAIN						 *
* A8 = PTR TO OBJECT THAT HIT IT					 *
*                                                                        *
**************************************************************************
PORCOLR
	MOVE	*A8(OID),A9,W		;GET KILLERS I.D.
	MOVI	DUMCOLL,A14
	MOVE	A14,*A0(OCVECT),L	;COLLISION POINTER NULL FOR NOW
	MOVE	*A0(OPLINK),A0,L	;GET THE PROCESS
	CALLA	GETA8			;GET THE DRIVER PTR
	CALLA	GETA10			;PRESERVE CORRECT STATE FOR CDELALL
	MOVE	A13,B0
	MOVE	A0,A13
	CALLA	CARSTOP
  	CALLA	CDELALL			;DELETE ALL PORSCHE PARTS
	MOVE	B0,A13
	CALLA	KILL
	MOVE	A10,A8			;AND PASS IT TO THE PROCESS
	CREATE	0EDH,POREXP		;CREATE THE PORSCHE EXPLODE PROC.
	MOVI	CVSDOFF,A0
	CALLA	ONESND			;TURN OFF THE CAR IDLE SOUND
	MOVE	@PORCNT,A0,W
	DEC	A0
	MOVE	A0,@PORCNT,W
	CLR	A0
	CLRC
	RETS

**************************************************************************
*                                                                        *
* PTRANCOL - PORSCHE TRANSPORT, COLLISION WITH ROCKET BOMB		 *
* A0 = PTR TO PORSCHE MAIN						 *
* A8 = PTR TO OBJECT THAT HIT IT					 *
*                                                                        *
**************************************************************************
PTRANCOL
	MOVI	DUMCOLL,A14
	MOVE	A14,*A0(OCVECT),L	;COLLISION POINTER NULL FOR NOW
	MOVE	*A8(OID),A9,W		;GET THE I.D. FOR SAFE KEEPING
	MOVE	*A0(OPLINK),A0,L	;GET THE PROCESS
	CALLA	GETA10			;PRESERVE CORRECT STATE FOR CDELALL
	CALLA	GETA8			;GET THE DRIVER PTR
	JRZ	PTRANC1			;NO DRIVER TO LAUNCH

	MOVE	*A0(PSPTR),A5,L		;GET THE PROCESS STACK PTR
	ADDI	SLPSTCK,A5		;OFFSET TO REAL STACK DATA
	CALLA	GETA11			
	MOVE	A0,B1			;STORE HERE
	MOVE	A8,B0			;AND HERE
	MOVE	A10,A8			;PREPARE TO LAUNCH PLAYER
	CALLA	GETANIX
	PUSH	A9
	CMPI	P1DATA,A11
	JRNE	PTRANC2
	MOVI	P1CTRL,A9
	JRUC	PTRANC3
PTRANC2
	MOVI	P2CTRL,A9	
PTRANC3
	MOVI	PJUMP|PRYTE,A7
	MOVE	A7,*A9,W
	MMFM	A5,A3,A4
	MOVE	*A3(OXPOS),A5,W			;WARP PLAYER TO DRIVER SPOT	
	MOVE	A0,*A3(OXPOS),W
	SUB	A5,A0
	MOVE	*A4(OXPOS),A5,W
	ADD	A0,A5
	MOVE	A5,*A4(OXPOS),W

	MOVE	A3,A0
	MOVI	PLAYERS,A1						 
	CALLA	INSOBJ			;INSERT TORSO			 
	CALLA	ADDSUPP			;ADD TO SUPPLEMENTAL LIST	 
	MOVE	A4,A0							 
	CALLA	INSOBJ			;INSERT LEGS 			 
	CALLA	ADDSUPP			;ADD TO SUPPLEMENTAL LIST	 
	MOVE	A3,A8
	CALLA	SHADST2			;START SHADOW FOR PLAYER TORSO	 
	MOVE	A4,A8							 
	CALLA	SHADST			;START SHADOW FOR PLAYER LEGS	 
	CREATE	INDPID,PTRANWW
	MOVE	B1,A0
	MOVE	B0,A8
	PULL	A9
PTRANC1
	MOVE	A13,B0
	MOVE	A0,A13
	CALLA	CARSTOP
  	CALLA	CDELALL			;DELETE ALL PORSCHE PARTS
	MOVE	B0,A13
	CALLA	KILL
	MOVE	A10,A8			;AND PASS IT TO THE PROCESS
	CREATE	0EDH,POREXP		;CREATE THE PORSCHE EXPLODE PROC.
	MOVI	CVSDOFF,A0
	CALLA	ONESND			;TURN OFF THE CAR IDLE SOUND
	MOVE	@PORCNT,A0,W
	DEC	A0
	MOVE	A0,@PORCNT,W
	CLR	A0
	CLRC
	RETS


**************************************************************************
*                                                                        *
* PCDRIVLP - DRIVEABLE PORSCHE COLLISION WITH A LAMP POST		 *
* A0 = PTR TO PORSCHE MAIN						 *
* A8 = PTR TO OBJECT THAT HIT IT					 *
*                                                                        *
**************************************************************************
PCDRIVLP
	MOVE	*A0(OXVEL),A14,L	;GET THE X VELOCITY
	MOVE	A14,A1
	ABS	A14
	CMPI	PORFULL-[4,0],A14
	JRHI	PCDRIVM0		;SLOW ENOUGH TO BOUNCE, SO BOUNCE
	JRUC	PCDRIVD0		;BLOW UP S.V.P.
**************************************************************************
*                                                                        *
* PCDRIVDC - DRIVEABLE PORSCHE COLLISION WITH A DUMPSTER		 *
* A0 = PTR TO PORSCHE MAIN						 *
* A8 = PTR TO OBJECT THAT HIT IT					 *
*                                                                        *
**************************************************************************
PCDRIVDC
	MOVE	*A0(OXVEL),A14,L	;GET THE X VELOCITY
	MOVE	A14,A1
	ABS	A14
	CMPI	PORFULL-[4,0],A14
	JRLS	PCDRIVDX		;SLOW ENOUGH TO BOUNCE, DUMPSTER HANDLE
PCDRIVD0
	MOVE	*A0(OPLINK),A14,L
	JRZ	PCDRIVRC		;BR = NO PROC
	MOVE	A0,B0
	MOVE	A8,B1
	SRA	3,A1			;DIVIDE DOWN X VELOCITY
	MOVE	A14,A0
	CALLA	GETA8	
	JRZ	PCDRID00		;BR = NO DRIVER
	MOVE	A1,*A8(OXVEL),L
PCDRID00	
	MOVE	*A0(PASSPTR),A8,L
	JRZ	PCDRID01
	MOVE	A1,*A8(OXVEL),L
PCDRID01
	MOVE	B1,A8
	SOUND1	PCDMPSND		;MAKE SOME NOISE
	MOVE	B0,A0
	CALLA	GIBOX			;GET THE INTERSECTION BOX
	MOVE	A1,A11			;PASS THE UPPER LEFT TO ROCKET MAN
	MOVE	A0,A8
	MOVE	*A8(OZPOS),A9,W
	MOVE	*A8(OFLAGS),A0,W
	SLL	16,A0
	MOVY	A0,A9
	MOVI	BEOID,A10
	CREATE	CEXPID,CEXPLODE
	MOVE	B1,A8
	MOVE	B0,A0	
	JRUC	PCDRIVRC		;BLOW THIS BAD DOG

*
*ENTRY POINT JUST MIRRORING PORSCHE VELOCITIES
*A0 = PTR TO PORSCHE, A8 = PTR TO OBJECT COLLIDED WITH
PCDRIVMV
	MOVE	*A0(OXVEL),A1,L
PCDRIVM0
	MOVE	A1,A1
	JRN	PCDRIVDA
	CALLA	WHICHSID
	JRC	PCDRIVDX		;BR = GOING RIGHT ON THE RIGHT, CAN IT
	JRUC	PCDRIVDB
PCDRIVDA
	CALLA	WHICHSID
	JRNC	PCDRIVDX		;BR = GOING LEFT ON THE LEFT, CAN IT
PCDRIVDB
	NEG	A1				
	SRA	1,A1
	MOVE	*A0(OZVEL),A2,L
	MOVE	*A0(OYVEL),A3,L
	SUB	A2,A3
	NEG	A2
	SRA	1,A2
	ADD	A2,A3
	MOVE	A13,B0
	MOVE	*A0(OPLINK),A13,L	;GET THE PROCESS
	JRZ	PCDRIVD1		;BR = NO PROCESS
	MOVE	A13,A0
	CALLA	GETA10			;PRESERVE CORRECT STATE FOR CDELALL
	CALLA	GETA8			;GET THE DRIVER PTR
	CALLA	CARXVEL			;MIRROR/2 X VELOCITY
	MOVE	A2,A1
	CALLA	CARZVEL			;MIRROR/2 Z VELOCITY
	MOVE	A3,A1
	CALLA	CARYVEL			;SUBSEQUENT Y VELOCITY
PCDRIVD1
	MOVE	B0,A13
PCDRIVDX
	CLR	A0
	CLRC
	RETS

**************************************************************************
*                                                                        *
* PCDRIVRC - DRIVEABLE PORSCHE COLLISION WITH A ROCKET		 	 *
* A0 = PTR TO PORSCHE MAIN						 *
* A8 = PTR TO OBJECT THAT HIT IT					 *
*                                                                        *
**************************************************************************
*
*INTERSECTION WITH A PLAYER ONE ROCKET
PC1DRIVR
	MOVE	@P1DATA+PIRQFLAG,A14,W
	ANDI	MDRIVER|MPASSEN,A14
	JRNZ	PCDRIVRX		;DON'T BLOW UP THIS CAR, I'M IN IT
	JRUC	PCDRIVRC
*
*INTERSECTION WITH A PLAYER ONE ROCKET
PC2DRIVR
	MOVE	@P2DATA+PIRQFLAG,A14,W
	ANDI	MDRIVER|MPASSEN,A14
	JRNZ	PCDRIVRX		;DON'T BLOW UP THIS CAR, I'M IN IT
PCDRIVRC
	MOVI	DUMCOLL,A14
	MOVE	A14,*A0(OCVECT),L	;COLLISION POINTER NULL FOR NOW
	MOVE	*A8(OID),A9,W		;AND ZE I.D. PLEASE
	MOVE	*A0(OPLINK),A0,L	;GET THE PROCESS
	CALLA	GETA10			;PRESERVE CORRECT STATE FOR CDELALL
	CLR	A14
	MOVE	A14,*A10(OPLINK),L	;SHOW NO CONTACT
	CALLA	GETA8			;GET THE DRIVER PTR
	JRZ	PCDRIVR1		;NO DRIVER TO LAUNCH
	MOVE	*A8(OID),A11,W
	MOVE	A0,B0
	MOVE	A8,A0
	CALLA	OBJON			;TURN THIS GUY ON FOR SURE
	CREATE	INDPID,PDRIVXWW
	MOVE	B0,A0
	CLR	A8
	MOVE	A8,@PLAYCAR,L
PCDRIVR1
	MOVE	*A0(PASSPTR),A3,L
	JRZ	PCDRIVR2
	MOVE	*A3(OID),A11,W
	MOVE	A0,B0
	MOVE	A3,A0
	CALLA	OBJON			;OH PLEASE, TURN ME ON TOO.
	CREATE	INDPID,PDRIVXWW
	MOVE	B0,A0
	CLR	A3
	MOVE	A3,*A0(PASSPTR),L
	MOVE	A3,@PLAYCAR,L
PCDRIVR2
	MOVE	A13,B0
	MOVE	A0,A13
	CALLA	CARSTOP
  	CALLA	CDELALL			;DELETE ALL PORSCHE PARTS
	MOVE	B0,A13
	CALLA	KILL
	MOVE	A10,A8			;AND PASS IT TO THE PROCESS
	CREATE	0EDH,POREXP		;CREATE THE PORSCHE EXPLODE PROC.
	MOVI	CVSDOFF,A0
	CALLA	ONESND			;TURN OFF THE CAR IDLE SOUND
	MOVE	@PORCNT,A0,W
	DEC	A0
	MOVE	A0,@PORCNT,W
PCDRIVRX
	CLR	A0
	CLRC
	RETS

*
*PDRIVXWW - DRIVEABLE PORSCHE WALK WATCHER PROCESS
*A11 = PTR TO PxOID, CHANGES TO PxCTRL
PDRIVXWW
	CMPI	P1OID,A11
	JRNE	PDRIVXW1
	MOVI	P1CTRL,A11
	MOVI	P1DATA,A10
	JRUC	PDRIVXW2
PDRIVXW1
	CMPI	P2OID,A11
	JRNE	PDRIVXWX		;WHO KNOWS WHAT THE FUCK THIS IS
	MOVI	P2CTRL,A11
	MOVI	P2DATA,A10
PDRIVXW2
	MOVI	PJUMP|PRYTE,A0
	MOVE	A0,*A11,W
	SLEEP	3
	MOVE	*A10(PIRQFLAG),A0,W
	ANDI	MDRIVER|MPASSEN,A0	;CHECK TO SEE IF HE JUMPED OUT
	JRNE	PDRIVXW2		;BR = THE JUMP DIDN'T TAKE YET

	MOVE	*A10(POBJ),A8,L
	MOVE	*A10(PLOBJ),A9,L	;GET BOTH PLAYER OBJECT PTRS
	MOVE	*A8(OXVEL),A10,L	;GET THE CURRENT VELOCITY
	JRNN	PDXWWSLP

PDXWWNLP
	ADDI	3000H,A10
	MOVE	*A8(OXVEL),A1,L
	ABS	A1
	CMPI	[1,1000H],A1
	JRLE	PDXWWFIN
	MOVE	A10,*A8(OXVEL),L
	MOVE	A10,*A9(OXVEL),L
	SLOOP	1,PDXWWNLP

PDXWWSLP
	SUBI	3000H,A10
	MOVE	*A8(OXVEL),A1,L
	ABS	A1
	CMPI	[1,1000H],A1
	JRLE	PDXWWFIN
	MOVE	A10,*A8(OXVEL),L
	MOVE	A10,*A9(OXVEL),L
	SLOOP	1,PDXWWSLP

PDXWWFIN
	SLEEP	16
	CLR	A0
	MOVE	A0,*A11,W
PDRIVXWX
	DIE

**************************************************************************
*                                                                        *
* CWINBCOL - VEHICLE WINDSHIELD COLLISION WITH A BULLET			 *
* A0 = PTR TO WINDSHEILD						 *
* A8 = PTR TO THING THAT HIT IT						 *
*                                                                        *
**************************************************************************
CWINBCOL
	MOVE	*A8(OID),A11,W		
	MOVI	DUMCOLL,A14
	MOVE	A14,*A0(OCVECT),L	;SET UP A DUMB COLLISION
	MOVE	A0,A8			;PASS THE BLOWOUT OBJECT
	CREATE	0,BLOWSHLD		;BLOWOUT THE WINDSHIELD
	JREQ	CWINB1			;COULDN'T CREATE THE WINDSHIELD
	MOVE	A0,*A8(OPLINK),L
CWINB1
	CLRC				;DON'T DELETE
	CLR	A0
	RETS

*
*COLLISION LISTS
PORCOLL					;NORMAL PORSCHE COLLISION
	.WORD	B1OID|ROCKETID
	.LONG	PORCOLR
	.WORD	B1OID|BOMBID
	.LONG	PORCOLR
	.WORD	B2OID|ROCKETID
	.LONG	PORCOLR
	.WORD	B2OID|BOMBID
	.LONG	PORCOLR
	.WORD	BEOID|ROCKETID
	.LONG	PORCOLR
	.WORD	BEOID|LANDMOID
	.LONG	PORCOLR
	.WORD	BEOID|BOMBID
	.LONG	PORCOLR
	.WORD	BEOID|DYNEXPID
	.LONG	PORCOLR
	.WORD	0
	.LONG	DUMRTSG

PTCOLL					;PORSCHE TRANSPORT COLLISION
	.WORD	B1OID|ROCKETID
	.LONG	PTRANCOL
	.WORD	B2OID|ROCKETID
	.LONG	PTRANCOL
	.WORD	BEOID|ROCKETID
	.LONG	PTRANCOL
	.WORD	BEOID|LANDMOID
	.LONG	PTRANCOL
	.WORD	BEOID|BOMBID
	.LONG	PTRANCOL	
	.WORD	BEOID|DYNEXPID
	.LONG	PTRANCOL
	.WORD	0
	.LONG	DUMRTSG

PDRVCOLL				;DRIVEABLE PORSCHE COLLISION
	.WORD	B1OID|ROCKETID
	.LONG	PC1DRIVR
	.WORD	B2OID|ROCKETID
	.LONG	PC2DRIVR
	.WORD	BEOID|ROCKETID
	.LONG	PCDRIVRC
	.WORD	BEOID|LANDMOID
	.LONG	PCDRIVRC
	.WORD	BEOID|BOMBID
	.LONG	PCDRIVRC	
	.WORD	BEOID|DYNEXPID
	.LONG	PCDRIVRC
	.WORD	BGDMPID
	.LONG	PCDRIVDC
	.WORD	DUMPOID
	.LONG	PCDRIVMV
	.WORD	BGLID
	.LONG	PCDRIVLP
	.WORD	0
	.LONG	DUMRTSG

PWINCOL
	.WORD	B1OID|BULLETID
	.LONG	CWINBCOL
	.WORD	B2OID|BULLETID
	.LONG	CWINBCOL
	.WORD	BEOID|BULLETID
	.LONG	CWINBCOL
	.WORD	B1OID|ROCKETID
	.LONG	CWINBCOL
	.WORD	B2OID|ROCKETID
	.LONG	CWINBCOL
	.WORD	BEOID|ROCKETID
	.LONG	CWINBCOL
	.WORD	0
	.LONG	DUMRTSG
PWHLCOL
	.WORD	0
	.LONG	DUMRTSG

PTNKCOL
	.WORD	0
	.LONG	DUMRTSG

**** DATA SECTION ****
	.data
*
*PORSCHE ANIMATION TABLES
*
D1OFF	.WORD	1,0,2,6,15,15,0,0,0,0,0,0,0,0,0,0,31,32,33,34,35,36,0,0,37
	.WORD	38,39,40,41,42,43,44

*PLAYER 1 DRIVING THE PORSCHE, PLAYER 2 PASSENGER
*	 FIRST LONG WORD IS THE PTR TO THE FRAME OFFSETS
*
PORP1P2
	.LONG	D1OFF
	.LONG	P1DRIVE,D1DETH,D1DETH,LOFDEXP,LOFDEXP1

	.LONG	LOFRAG1,LOFRAG2,LOFRAG2,LOFRAG3
	.LONG	LOFRAG1,LOFRAG2,LOFRAG2,LOFRAG3
	.LONG	LOFRAG4

	.LONG	P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE
	.LONG	P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE,P2PFIRE

	.LONG	P1RTURN,P1LTURN,PORRWHL,PORFWHL,PWHLLEFT,PWHLRYTE	
	.LONG	PFSPIN,PFSPINF,PRSPIN,PRSPINF,PORRWHLR,PORFWHLR
	.LONG	PWHLLR,PWHLRR

P1DRIVE					;PLAYER 1 DRIVING ANIMATION
	.LONG	jtdrive1
	.WORD	1
	LONGNULL

P1RTURN					;PLAYER 1 TURN RIGHT
	.LONG	jtdrive1
	.WORD	1
	.LONG	0			;BREAK 'TIL TURN BACK
	.LONG	jtdrive1
	.WORD	1
	.LONG	0

P1LTURN					;PLAYER 1 TURN LEFT
	.LONG	jtdrive1
	.WORD	1
	.LONG	0
	.LONG	jtdrive1
	.WORD	1
	.LONG	0

P2PFIRE		 			;PLAYER 2 FIRE WHILE RIDING
	.LONG	ytpassen1
	.WORD	1
	.LONG	0

*PLAYER 2 DRIVING THE PORSCHE, PLAYER 1 PASSENGER
*	 FIRST LONG WORD IS THE PTR TO THE FRAME OFFSETS
*
PORP2P1
	.LONG	D1OFF
	.LONG	P2DRIVE,D1DETH,D1DETH,LOFDEXP,LOFDEXP1

	.LONG	LOFRAG1,LOFRAG2,LOFRAG2,LOFRAG3
	.LONG	LOFRAG1,LOFRAG2,LOFRAG2,LOFRAG3
	.LONG	LOFRAG4

	.LONG	P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE
	.LONG	P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE,P1PFIRE

	.LONG	P2RTURN,P2LTURN,PORRWHL,PORFWHL,PWHLLEFT,PWHLRYTE	
	.LONG	PFSPIN,PFSPINF,PRSPIN,PRSPINF,PORRWHLR,PORFWHLR
	.LONG	PWHLLR,PWHLRR

P2DRIVE					;PLAYER 2 DRIVING ANIMATION
	.LONG	ytdrive1
	.WORD	1
	LONGNULL

P2RTURN					;PLAYER 2 TURN RIGHT
	.LONG	jtdrive1
	.WORD	1
	.LONG	0			;BREAK 'TIL TURN BACK
	.LONG	ytdrive1
	.WORD	1
	.LONG	0

P2LTURN					;PLAYER 2 TURN LEFT
	.LONG	jtdrive1
	.WORD	1
	.LONG	0
	.LONG	ytdrive1
	.WORD	1
	.LONG	0

P1PFIRE		 			;PLAYER 1 FIRE WHILE RIDING
	.LONG	jtpassen1
	.WORD	1
	.LONG	0

D1DETH	
LOFRAG1
LOFRAG2
LOFRAG3
LOFRAG4
LOFDEXP:
LOFDEXP1:
	LONGNULL

POREXPFR		 		;PORSCHE EXPLOSION
	.LONG	PORCRASH1
	.WORD	12
	.LONG	PORCRASH1
	.WORD	8
	.LONG	PORCRASH1
	.WORD	8
	LONGNULL

PORFWHL			;PORSCHE FRONT WHEEL STRAIGHT ROTATE FORWARD
	.LONG	PORWHL1F
	.WORD	1
	.LONG	PORWHL2F
	.WORD	1
	.LONG	PORWHL3F
	.WORD	1
	.LONG	PORWHL4F
	.WORD	1
	.LONG	0

PORRWHL			;PORSCHE REAR WHEEL STRAIGHT ROTATE FORWARD
	.LONG	PORWHL1R
	.WORD	1
	.LONG	PORWHL2R
	.WORD	1
	.LONG	PORWHL3R
	.WORD	1
	.LONG	PORWHL4R
	.WORD	1
	.LONG	0

PORFWHLR			;PORSCHE FRONT WHEEL STRAIGHT ROTATE REVERSE
	.LONG	PORWHL4F
	.WORD	1
	.LONG	PORWHL3F
	.WORD	1
	.LONG	PORWHL2F
	.WORD	1
	.LONG	PORWHL1F
	.WORD	1
	.LONG	0

PORRWHLR			;PORSCHE REAR WHEEL STRAIGHT ROTATE REVERSE
	.LONG	PORWHL4R
	.WORD	1
	.LONG	PORWHL3R
	.WORD	1
	.LONG	PORWHL2R
	.WORD	1
	.LONG	PORWHL1R
	.WORD	1
	.LONG	0

PORWEEL1CC			;BOUNCY EXPLOSION WHEELS
	.LONG	PORWHL1C
	.WORD	1|NEWPALET
	.LONG	PORSHPAL
	.LONG	PORWHL2C
	.WORD	1
	.LONG	PORWHL3C
	.WORD	1
	.LONG	PORWHL4C
	.WORD	1
	.LONG	0
	
PWHLRYTE			;PORSCHE WHEELS TURN RYTE FORWARD SPIN
	.LONG	PRYTEWHL1
	.WORD	1
	.LONG	PRYTEWHL2
	.WORD	1
	.LONG	PRYTEWHL3
	.WORD	1
	.LONG	PRYTEWHL4
	.WORD	1
	.LONG	0

PWHLLEFT			;PORSCHE FRONT WHEELS LEFT FORWARD SPIN
	.LONG	PLEFTWHL1
	.WORD	1
	.LONG	PLEFTWHL2
	.WORD	1
	.LONG	PLEFTWHL3
	.WORD	1
	.LONG	PLEFTWHL4
	.WORD	1
	.LONG	0

PWHLRR				;PORSCHE WHEELS TURN RYTE REVERSE SPIN
	.LONG	PRYTEWHL4
	.WORD	1
	.LONG	PRYTEWHL3
	.WORD	1
	.LONG	PRYTEWHL2
	.WORD	1
	.LONG	PRYTEWHL1
	.WORD	1
	.LONG	0

PWHLLR				;PORSCHE FRONT WHEELS LEFT REVERSE SPIN
	.LONG	PLEFTWHL4
	.WORD	1
	.LONG	PLEFTWHL3
	.WORD	1
	.LONG	PLEFTWHL2
	.WORD	1
	.LONG	PLEFTWHL1
	.WORD	1
	.LONG	0

PBLOWWS		 		;PORSCHE WINDSHIELD BLOWOUT
	.LONG	PORSHAT1
	.WORD	7
	.LONG	PORSHAT2
	.WORD	6
	.LONG	PORSHAT3
	.WORD	6
	.LONG	PORSHAT4
	.WORD	5
	.LONG	PORSHAT5
	.WORD	4
	LONGNULL

PFSPIN				;PORSCHE FORWARD 180 UNFLIPPED	
	.LONG	PORFORS60
	.WORD	7
	.LONG	PORFORS90
	.WORD	6
	.LONG	PORFORS60
	.WORD	FLIPBITS|7,M_FLIPH
	.LONG	PORSCHE1
	.WORD	2
	.LONG	0

PFSPINF				;PORSCHE FORWARD 180, FLIPPED	
	.LONG	PORFORS60
	.WORD	7
	.LONG	PORFORS90
	.WORD	6
	.LONG	PORFORS60
	.WORD	FLIPBITS|7,0
	.LONG	PORSCHE1
	.WORD	2
	.LONG	0

PRSPIN				;PORSCHE REVERSE 180 UNFLIPPED	
	.LONG	PORREVS60
	.WORD	7
	.LONG	PORREVS90
	.WORD	6
	.LONG	PORREVS60
	.WORD	FLIPBITS|7,M_FLIPH
	.LONG	PORSCHE1
	.WORD	2
	.LONG	0

PRSPINF		 		;PORSCHE REVERSE 180, FLIPPED
	.LONG	PORREVS60
	.WORD	7
	.LONG	PORREVS90
	.WORD	6
	.LONG	PORREVS60
	.WORD	FLIPBITS|7,0
	.LONG	PORSCHE1
	.WORD	2
	.LONG	0

CDOORA				;SPINNING CAR DOOR ANIMATION
	.LONG	CDOOR1
	.WORD	5|NEWPALET
	.LONG	PORSHPAL
	.LONG	CDOOR2
	.WORD	5
	.LONG	CDOOR3
	.WORD	5
	.LONG	CDOOR4
	.WORD	5
	.LONG	CDOOR5
	.WORD	5
	.LONG	CDOOR1
	.WORD	5
	.LONG	CDOOR2
	.WORD	5
	.LONG	CDOOR3
	.WORD	5
	.LONG	CDOOR4
	.WORD	5
	.LONG	CDOOR5
	.WORD	5
	LONGNULL

CSHRAPA					;PORSCHE SHRAPNEL ANIMATION
	.LONG	CSHRAP1
	.WORD	5|NEWPALET
	.LONG	PORSHPAL
	.LONG	CSHRAP2
	.WORD	5
	.LONG	CSHRAP3
	.WORD	5
	.LONG	CSHRAP4
	.WORD	5
	.LONG	CSHRAP5
	.WORD	5
	.LONG	CSHRAP1
	.WORD	5
	.LONG	CSHRAP2
	.WORD	5
	.LONG	CSHRAP3
	.WORD	5
	.LONG	CSHRAP4
	.WORD	5
	.LONG	CSHRAP5
	.WORD	5
	.LONG	CSHRAP1
	.WORD	5
	.LONG	CSHRAP2
	.WORD	5
	.LONG	CSHRAP3
	.WORD	5
	.LONG	CSHRAP4
	.WORD	5
	.LONG	CSHRAP5
	.WORD	5
	LONGNULL

CSHRAPB					;PORSCHE FRAGMENT ANIMATION
	.LONG	CFRAG1
	.WORD	5|NEWPALET
	.LONG	PORSHPAL
	.LONG	CFRAG2
	.WORD	5
	.LONG	CFRAG3
	.WORD	5
	.LONG	CFRAG4
	.WORD	5
	.LONG	CFRAG5
	.WORD	5
	.LONG	CFRAG1
	.WORD	5
	.LONG	CFRAG2
	.WORD	5
	.LONG	CFRAG3
	.WORD	5
	.LONG	CFRAG4
	.WORD	5
	.LONG	CFRAG5
	.WORD	5
	.LONG	CFRAG1
	.WORD	5
	.LONG	CFRAG2
	.WORD	5
	.LONG	CFRAG3
	.WORD	5
	.LONG	CFRAG4
	.WORD	5
	.LONG	CFRAG5
	.WORD	5
	LONGNULL

*
*THE INTRO PORSCHE INITIALIZATION TABLE
PORINITI
	.LONG	0,0,256*10000H,200*Y
	.WORD	0,0
	.LONG	PORSCHE1,PORZ,PORCOLL
	.WORD	DMAWNZ,INTR1OID|CBODYID
	.LONG	0

	.LONG	0,0,256*10000H,200*Y
	.WORD	0,0
	.LONG	POR1WIN1,PORZ,PWINCOL
	.WORD	DMAWNZ,INTR1OID|WSHLDID
	.LONG	0

	.LONG	7FFF7FFFH		 	;SKIP ETEMP2

	.LONG	0,0,256*10000H,200*Y
	.WORD	0,0
	.LONG	PORWHL1F,PORWHLZ,PWHLCOL
	.WORD	DMAWNZ,INTR1OID|FTIREID
	.LONG	0

	.LONG	0,0,256*10000H,200*Y
	.WORD	0,0
	.LONG	PORWHL3R,PORWHLZ,PWHLCOL
	.WORD	DMAWNZ,INTR1OID|RTIREID
	.LONG	0

	.LONG	7FFF7FFFH			;SKIP ETEMP5
	.LONG	7FFF7FFFH			;SKIP ETEMP6
	.LONG	7FFF7FFFH			;SKIP ETEMP7

	.LONG	0,0,256*10000H,200*Y		;ETEMP8 IS PASSENGER
	.WORD	0,0
	.LONG	jtpassen1,PZSIZ,DUMCOLL
	.WORD	DMAWNZ,P1PLACE
	.LONG	0

	.LONG	0,0,256*10000H,200*Y
	.WORD	0,0
	.LONG	ytdrive1,PZSIZ,DUMCOLL
	.WORD	DMAWNZ,P2PLACE
	.LONG	0

*
*THE PORSCHE TRANSPORT PLAYER 1
PORTRAN1
	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORSCHE1,PORZ,PTCOLL
	.WORD	DMAWNZ,CSTR1OID|CBODYID
	.LONG	0

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	POR1WIN1,PORZ,PWINCOL
	.WORD	DMAWNZ,CSTR1OID|WSHLDID
	.LONG	0

	.LONG	7FFF7FFFH		 	;SKIP ETEMP2

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORWHL1F,PORWHLZ,PWHLCOL
	.WORD	DMAWNZ,CSTR1OID|FTIREID
	.LONG	0

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORWHL3R,PORWHLZ,PWHLCOL
	.WORD	DMAWNZ,CSTR1OID|RTIREID
	.LONG	0

	.LONG	7FFF7FFFH			;SKIP ETEMP5
	.LONG	7FFF7FFFH			;SKIP ETEMP6
	.LONG	7FFF7FFFH			;SKIP ETEMP7
	.LONG	7FFF7FFFH			;SKIP ETEMP8

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	jtdrive1,PZSIZ,DUMCOLL
	.WORD	DMAWNZ,P1PLACE
	.LONG	0

*
*THE PORSCHE TRANSPORT PLAYER 2
PORTRAN2
	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORSCHE1,PORZ,PTCOLL
	.WORD	DMAWNZ,CSTR1OID|CBODYID
	.LONG	0

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	POR1WIN1,PORZ,PWINCOL
	.WORD	DMAWNZ,CSTR1OID|WSHLDID
	.LONG	0

	.LONG	7FFF7FFFH		 	;SKIP ETEMP2

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORWHL1F,PORWHLZ,PWHLCOL
	.WORD	DMAWNZ,CSTR1OID|FTIREID
	.LONG	0

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORWHL3R,PORWHLZ,PWHLCOL
	.WORD	DMAWNZ,CSTR1OID|RTIREID
	.LONG	0

	.LONG	7FFF7FFFH			;SKIP ETEMP5
	.LONG	7FFF7FFFH			;SKIP ETEMP6
	.LONG	7FFF7FFFH			;SKIP ETEMP7
	.LONG	7FFF7FFFH			;SKIP ETEMP8

	.LONG	0,0,PORTRANX*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	ytdrive1,PZSIZ,DUMCOLL
	.WORD	DMAWNZ,P2PLACE
	.LONG	0
*
*THE DRIVEABLE PORSCHE INIT TABLES
PORDINIT
	.LONG	0,0,PORDIXR*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORSCHE1,PORZ,PDRVCOLL
	.WORD	DMAWNZ,TRAN1OID|CBODYID
	.LONG	PORSBLIP

	.LONG	0,0,PORDIXR*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	POR1WIN1,PORZ,PWINCOL
	.WORD	DMAWNZ,TRAN1OID|WSHLDID
	.LONG	0

	.LONG	7FFF7FFFH		 	;SKIP ETEMP2

	.LONG	0,0,PORDIXR*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORWHL1F,PORWHLZ,DUMCOLL
	.WORD	DMAWNZ,TRAN1OID|FTIREID
	.LONG	0

	.LONG	0,0,PORDIXR*10000H,PORTRANY*Y
	.WORD	0,0
	.LONG	PORWHL2R,PORWHLZ,DUMCOLL
	.WORD	DMAWNZ,TRAN1OID|RTIREID
	.LONG	0

	.LONG	7FFF7FFFH			;SKIP ETEMP5
	.LONG	7FFF7FFFH			;SKIP ETEMP6
	.LONG	7FFF7FFFH			;SKIP ETEMP7
	.LONG	7FFF7FFFH			;SKIP ETEMP8

	.LONG	7FFF7FFFH			;YOU PROVIDE THE DRIVER

	.END
